; Script generated by the HM NIS Edit Script Wizard.
!include x64.nsh
!include StrReplace.nsh

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "GreenCubes"
!define PRODUCT_PUBLISHER "GreenCubes"
!define PRODUCT_WEB_SITE "https://greencubes.org"
!define PRODUCT_UNINST_ROOT_KEY "HKCU"
!define PRODUCT_UNINST_KEY "Software\GreenCubes"
!define PRODUCT_DIR_REGKEY "Software\GreenCubes"
;Get installation folder from registry if available
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""

SetCompressor lzma
RequestExecutionLevel admin

;--------------------------------
;Variables

Var StartMenuFolder

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "C:\_Greencubes\Git\GreenCubesLauncher\install\icon.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings

!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

;Pages
  ; Welcome page
    ;!insertmacro MUI_PAGE_WELCOME
  ; License page
    ;!insertmacro MUI_PAGE_LICENSE "c:\path\to\licence\YourSoftwareLicence.txt"
  ; Directory page
    !insertmacro MUI_PAGE_DIRECTORY
  ;Start Menu Folder Page
    !define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
    !define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
    !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"

    !insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder

  ; Instfiles page
    !insertmacro MUI_PAGE_INSTFILES

  ; Finish page
    !define MUI_FINISHPAGE_RUN "$INSTDIR\greencubes.exe"
    Function finishpageaction
      CreateShortcut "$DESKTOP\GreenCubes.lnk" "$INSTDIR\greencubes.exe"
    FunctionEnd

    !define MUI_FINISHPAGE_SHOWREADME ""
    !define MUI_FINISHPAGE_SHOWREADME_TEXT "Создать ярлык на Рабочем столе"
    !define MUI_FINISHPAGE_SHOWREADME_FUNCTION finishpageaction

    !insertmacro MUI_PAGE_FINISH

; Uninstaller pages
  !insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "Russian"
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME}"
OutFile "C:\_Greencubes\Git\GreenCubesLauncher\install\greencubes-setup.exe"
InstallDir "$PROGRAMFILES\GreenCubes"
ShowInstDetails show
ShowUnInstDetails show

Function .onInit
  ${If} ${RunningX64}
    StrCpy $INSTDIR "$PROGRAMFILES64\${PRODUCT_NAME}"
  ${EndIf}
  ;!insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

Section "MainSection" SEC01
  AddSize 1048576 ; Add 1Gb as we don't have files in section
  SetOutPath "$INSTDIR"
  inetc::get /RESUME "" /QUESTION $(MUI_TEXT_ABORTWARNING) "https://greencubes.org/client/windows-installer.zip" "package.zip"
    Pop $0
    StrCmp $0 "OK" dlok
    Abort
  dlok:
  nsUnzip::Extract / "package.zip" /END
  ; TODO : Check sucess
  Delete "package.zip"
  WriteUninstaller $INSTDIR\uninstall.exe
SectionEnd

Section -Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    ;Create shortcuts
    CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
    CreateShortcut "$SMPROGRAMS\$StartMenuFolder\GreenCubes.lnk" "$INSTDIR\greencubes.exe"
    CreateShortcut "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -CreateUninstall
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\greencubes.exe"
SectionEnd

Section "un.MainSection"
  Delete "$INSTDIR\Uninstall.exe"

  ;Remove downloaded files
  ClearErrors
  nsJSON::Set /file $INSTDIR\version.json
  ${If} ${Errors}
    goto skipjson
  ${EndIf}
  nsJSON::Get /count "files" /end
  ${If} ${Errors}
    goto skipjson
  ${EndIf}
  Pop $R0
  ${For} $R1 0 $R0
    nsJSON::Get `files` /index $R1 `name` /end
    Pop $R2
    !insertmacro ReplaceSubStr $R2 "/" "\"
    Delete "$INSTDIR\$R2"
  ${Next}
  skipjson:
  
  ; Remove folders
  RMDir /r "$INSTDIR\jre"
  RMDir /r "$INSTDIR\resources"
  RMDir /r "$INSTDIR\cache"
  RMDir /r "$INSTDIR\libraries"
  RMDir /r "$INSTDIR\patch"
  ; Remove installed files
  Delete "$INSTDIR\debug.bat"
  Delete "$INSTDIR\greencubes.exe"
  Delete "$INSTDIR\greencubes.jar"
  Delete "$INSTDIR\patcher.jr"
  Delete "$INSTDIR\greencubes.sh"
  Delete "$INSTDIR\NOTICE.txt"
  ; Remove some files that could be created in work
  Delete "$INSTDIR\version.json"
  Delete "$INSTDIR\user.dat"
  Delete "$INSTDIR\launcherUpdateError.txt"
  Delete "$INSTDIR\lwjgl64.dll"
  Delete "$INSTDIR\lwjgl32.dll"
  Delete "$INSTDIR\OpenAL64.dll"
  Delete "$INSTDIR\OpenAL32.dll"
  Delete "$INSTDIR\patch.json"
  
  RMDir "$INSTDIR" ; Remove install dir only if it is empty now (in most cases it's not)
  
  ; Remove shortcuts
  !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuFolder
  Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\GreenCubes.lnk"
  Delete "$DESKTOP\GreenCubes.lnk"
  RMDir "$SMPROGRAMS\$StartMenuFolder"
  
  ; Clear registry
  DeleteRegKey HKCU "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
SectionEnd