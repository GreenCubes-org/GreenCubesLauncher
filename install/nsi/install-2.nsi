; Script generated by the HM NIS Edit Script Wizard.
!include x64.nsh
!include StrReplace.nsh
!include IsUserAdmin.nsh

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "GreenCubes"
!define PRODUCT_PUBLISHER "GreenCubes"
!define PRODUCT_WEB_SITE "https://greencubes.org"
!define PRODUCT_UNINST_ROOT_KEY "HKCU"
!define PRODUCT_UNINST_KEY "Software\GreenCubes"
!define PRODUCT_DIR_REGKEY "Software\GreenCubes"
!define REG_UNINSTALL "Software\Microsoft\Windows\CurrentVersion\Uninstall\GreenCubes"
;Get installation folder from registry if available
InstallDirRegKey HKCU "${PRODUCT_DIR_REGKEY}" ""

SetCompressor lzma
RequestExecutionLevel user ; Re-ask admin permissions so uninstaller runs under the same user as installer

;--------------------------------
;Variables

Var StartMenuFolder

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "C:\_Greencubes\Git\GreenCubesLauncher\install\icon.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings

!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

;Pages
  ; Welcome page
    ;!insertmacro MUI_PAGE_WELCOME
  ; License page
    ;!insertmacro MUI_PAGE_LICENSE "c:\path\to\licence\YourSoftwareLicence.txt"
  ; Directory page
    !insertmacro MUI_PAGE_DIRECTORY
  ;Start Menu Folder Page
    !define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
    !define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
    !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"

    !insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder

  ; Instfiles page
    !insertmacro MUI_PAGE_INSTFILES

  ; Finish page
    !define MUI_FINISHPAGE_RUN "$INSTDIR\greencubes.exe"
    Function finishpageaction
      CreateShortcut "$DESKTOP\GreenCubes.lnk" "$INSTDIR\greencubes.exe"
    FunctionEnd

    !define MUI_FINISHPAGE_SHOWREADME ""
    !define MUI_FINISHPAGE_SHOWREADME_TEXT "Создать ярлык на Рабочем столе"
    !define MUI_FINISHPAGE_SHOWREADME_FUNCTION finishpageaction

    !insertmacro MUI_PAGE_FINISH

; Uninstaller pages
  !insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "Russian"
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

; MUI end ------

Name "${PRODUCT_NAME}"
OutFile "C:\_Greencubes\Git\GreenCubesLauncher\install\GreenCubes-setup.exe"
InstallDir "$PROGRAMFILES\GreenCubes"
ShowInstDetails show
ShowUnInstDetails show

Function .onInit
  ; If running on x64 systems, install to Program Files, not Program Files (x86)
  ${If} ${RunningX64}
    StrCpy $INSTDIR "$PROGRAMFILES64\${PRODUCT_NAME}"
  ${EndIf}
  !insertmacro IsUserAdmin $0
  ${If} $0 <> 1
    StrCpy $INSTDIR "$LOCALAPPDATA\${PRODUCT_NAME}"
  ${EndIf}
  ;!insertmacro MUI_LANGDLL_DISPLAY
FunctionEnd

Section "MainSection" SEC01
  AddSize 1048576 ; Add 1Gb as we don't have files in section
  SetOutPath "$INSTDIR"
  DetailPrint "Загрузка файла package.zip..."
  ; Download main package
  inetc::get /RESUME "" /QUESTION $(MUI_TEXT_ABORTWARNING) "https://greencubes.org/client/windows-installer.zip" "package.zip"
    Pop $0
    StrCmp $0 "OK" dlok1
    goto error
  dlok1:
  DetailPrint "Распаковка файла package.zip..."
  nsUnzip::Extract / "package.zip" /END
    Pop $0
    IntCmp 0 $0 unzipok1
    goto error
  unzipok1:
  ; TODO : Check unpacking success
  
  ; Update launcher
  DetailPrint "Загрузка файла launcher.json..."
  inetc::get /RESUME "" /QUESTION $(MUI_TEXT_ABORTWARNING) "https://login.greencubes.org/files/launcher/version.json" "launcher.json"
    Pop $0
    StrCmp $0 "OK" dlok2
    goto error
  dlok2:
  DetailPrint "Обновление лаунчера..."
  nsJSON::Set /file $INSTDIR\launcher.json
    IfErrors skipupdate
  nsJSON::Get /count "files" /end
    IfErrors skipupdate
    Pop $R0
  ${For} $R1 0 $R0
    nsJSON::Get `files` /index $R1 `name` /end
      Pop $R2
    StrCpy $R3 $R2
    !insertmacro ReplaceSubStr $R3 "/" "\"
    DetailPrint "Загрузка файла $R3..."
    inetc::get /RESUME "" /QUESTION $(MUI_TEXT_ABORTWARNING) "https://login.greencubes.org/files/launcher/$R2" "$R3"
      Pop $0
      StrCmp $0 "OK" dlok3
      DetailPrint "Не удалось загрузить $R3"
    dlok3:
  ${Next}
  goto noerror
  
  skipupdate:
  DetailPrint "Обновление отменено из-за ошибки"
  goto noerror
  
  ; Process errors
  error:
    ; TODO : Add error page?
    DetailPrint "Ошибка при установке :( попробуйте повторить"
    Abort
  noerror:
  DetailPrint "Установка VC 2013 Redistributable"
  
  ; Clear
  Delete "launcher.json"
  Delete "package.zip"
  
  ; Create shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
    CreateShortcut "$SMPROGRAMS\$StartMenuFolder\GreenCubes.lnk" "$INSTDIR\greencubes.exe"
    CreateShortcut "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk" "$INSTDIR\uninstall.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
  
  ; Create uninstaller
  WriteUninstaller "$INSTDIR\uninstall.exe"
  WriteRegStr HKCU "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\greencubes.exe"
  ; Write uninstall in Prgorams and Components
  WriteRegStr HKCU "${REG_UNINSTALL}" "DisplayName" "${PRODUCT_NAME}"
  WriteRegStr HKCU "${REG_UNINSTALL}" "DisplayIcon" "$\"$INSTDIR\GreenCubes.exe$\""
  WriteRegStr HKCU "${REG_UNINSTALL}" "Publisher" "GreenCubes"
  WriteRegStr HKCU "${REG_UNINSTALL}" "DisplayVersion" "1.0.0.0"
  WriteRegDWord HKCU "${REG_UNINSTALL}" "EstimatedSize" 1048576 ;KB
  WriteRegStr HKCU "${REG_UNINSTALL}" "HelpLink" "https://help.greencubes.org"
  WriteRegStr HKCU "${REG_UNINSTALL}" "URLInfoAbout" "https://greencubes.org"
  WriteRegStr HKCU "${REG_UNINSTALL}" "InstallLocation" "$\"$INSTDIR$\""
  WriteRegStr HKCU "${REG_UNINSTALL}" "InstallSource" "$\"$EXEDIR$\""
  WriteRegDWord HKCU "${REG_UNINSTALL}" "NoModify" 1
  WriteRegDWord HKCU "${REG_UNINSTALL}" "NoRepair" 1
  WriteRegStr HKCU "${REG_UNINSTALL}" "UninstallString" "$\"$INSTDIR\uninstall.exe$\""
  WriteRegStr HKCU "${REG_UNINSTALL}" "Comments" "Uninstalls ${PRODUCT_NAME}."
SectionEnd

Section "un.MainSection"
  Delete "$INSTDIR\uninstall.exe"

  ;Remove downloaded files
  ClearErrors
  nsJSON::Set /file $INSTDIR\version.json
    IfErrors skipjson
  nsJSON::Get /count "files" /end
    IfErrors skipjson
    Pop $R0
  ${For} $R1 0 $R0
    nsJSON::Get `files` /index $R1 `name` /end
      Pop $R2
    !insertmacro ReplaceSubStr $R2 "/" "\"
    Delete "$INSTDIR\$R2"
  ${Next}
  skipjson:
  
  ; Remove folders
  RMDir /r "$INSTDIR\jre"
  RMDir /r "$INSTDIR\resources"
  RMDir /r "$INSTDIR\cache"
  RMDir /r "$INSTDIR\libraries"
  RMDir /r "$INSTDIR\patch"
  ; Remove installed files
  Delete "$INSTDIR\debug.bat"
  Delete "$INSTDIR\greencubes.exe"
  Delete "$INSTDIR\greencubes.jar"
  Delete "$INSTDIR\patcher.jr"
  Delete "$INSTDIR\greencubes.sh"
  Delete "$INSTDIR\NOTICE.txt"
  Delete "$INSTDIR\javafx.jr"
  Delete "$INSTDIR\runx64.bat"
  Delete "$INSTDIR\runx86.bat"
  Delete "$INSTDIR\msvcr120.dll"
  Delete "$INSTDIR\msvcp120.dll"
  ; Remove some files that could be created in work
  Delete "$INSTDIR\version.json"
  Delete "$INSTDIR\user.dat"
  Delete "$INSTDIR\launcherUpdateError.txt"
  Delete "$INSTDIR\lwjgl64.dll"
  Delete "$INSTDIR\lwjgl32.dll"
  Delete "$INSTDIR\OpenAL64.dll"
  Delete "$INSTDIR\OpenAL32.dll"
  Delete "$INSTDIR\patch.json"
  Delete "$INSTDIR\enableold.txt"
  
  RMDir "$INSTDIR" ; Remove install dir only if it is empty now (in most cases it's not)
  
  ; Remove shortcuts
  !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuFolder
  Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\GreenCubes.lnk"
  Delete "$DESKTOP\GreenCubes.lnk"
  RMDir "$SMPROGRAMS\$StartMenuFolder"
  
  ; Clear registry
  DeleteRegKey HKCU "${REG_UNINSTALL}"
  DeleteRegKey HKCU "${PRODUCT_DIR_REGKEY}"
SectionEnd